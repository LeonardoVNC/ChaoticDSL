grammar edu.upb.lp.Chaotic with org.eclipse.xtext.common.Terminals

generate chaotic "http://www.upb.edu/lp/Chaotic"

//Estructura del programa
Program:	
	userSection = UserSection
	channelSection = ChannelSection
	execution = ChatSection
;

UserSection:
	{UserSection} 'Usuarios conectados:'
	(users += UserDeclaration+ | 'Vaya, parece que no hay nadie.')
;

ChannelSection:
	{ChannelSection} 'Canales disponibles:'
	(channels += ChannelOperation+ | 'Vaya, parece que no hay canales adicionales.')
;

ChatSection:
	{ChatSection} 'Chat #General'
	'Hola muchachada!'
	body=InstructionBody
	'Mañana hay examen de compilación, nos vimo :p'
;

//Gramática para UserSection, Variables
UserDeclaration:
	'Saluden a' '@' name=ID 'el nuevo miembro de' '$' type=DataType '.'
;

//Gramática para ChannelSection
ChannelOperation:
	'Se ha creado un nuevo canal llamado' 
	'#' name=ID	':'
	body=InstructionBody
	'Esperemos sea de su agrado.'
;

ParamField:
	'$' type=DataType 'también llamado' name=ID
;

InstructionBody:
	{InstructionBody} 
	(asignations+=UserAsignation | prints+=PrintLine | ifs+=IfInstruction | whiles+=WhileInstruction | channels+=ChannelCall)*
;

//Operaciones - Body
UserAsignation:
	user=[UserDeclaration] 'ha cambiado su descripción a:' value=Expression'.'
;

PrintLine:
	'Que tal todo ' value=[UserDeclaration]'?'
;

IfInstruction:
	'ATENCION! Si'
	'<' condition=Expression '>' 
	'procederemos a:' 
	body=InstructionBody
	'Gracias por su atención.' 
;

WhileInstruction:
	'AVISO! Mientras'
	'<'condition=Expression'>' 
	'se procederá a:'
	body=InstructionBody
	'Gracias por su atención.'
;

ChannelCall:
	'Nada que ver su conversacion, vayan a' 
	'#'name=[ChannelOperation] 
	'y dejen de molestar aca'
;

//Tipos de Datos
enum DataType:
	EnteroType = 'Enteros' | BoolType='Bools' | CadenasType = 'Cadenas' | DecType='Decimales'
;

//Expressions
Expression:
	(
		singleExpr = SingleExpression | 
		singleOpExpr = SingleOperatorExpression |
		userRef = UserDataReference |
		parenthesisExpr = ParenthesisExpression
	)
	(second = TempExpression)
;

TempExpression:
	(operador=PairOperator secondValue=Expression follow=TempExpression)?
;


SingleExpression:
	literal = (IntLiteral | DecLiteral | BoolLiteral | CadenasLiteral) 
;

enum PairOperator:
	Plus = '+' | Less = '-' | Mult = '*' | Div = '/' | And = 'y' | Or = 'o' | Equal = 'es igual a' | 
	Greater = 'le gana a' | GreaterP = 'le gane a' | GreaterEq = 'le podria ganar a' | 
	GreaterEqP = 'le pueda ganar a' | Concat = 'junto a'
;

enum SingleOperator:
	BoolNegation = 'Ignoren a ese' | IntNegative = 'Salio medio negativo el'
;

SingleOperatorExpression:
	operator = SingleOperator expression=Expression
;


UserDataReference:
	'@' user=[UserDeclaration]
;

ParenthesisExpression:
	'(' expression = Expression ')'
;

//Literals
IntLiteral:
	value = INT
;

DecLiteral:
	mainValue = INT '.' decimalValue=INT
;

BoolLiteral:
	value=('true' | 'false')
;

CadenasLiteral:
	value=STRING
;


//Comentarios
@Override 
terminal ML_COMMENT : 'Por cierto...' -> 'nada eso...';
@Override 
terminal SL_COMMENT : 'Ah si,' !('\n'|'\r')* ('\r'? '\n')?;
